mode := "debug"
build-flags := ""

export CARGO_INCREMENTAL := "0"

# Run using a specified local mode (can only be called on built packages)
@run +FLAGS='': (_package-complete)
    if [ {{mode}} = release ]; then \
      cargo run --offline --frozen --release -q -- {{FLAGS}}; \
    else \
      cargo run --offline --frozen --features debug_print -- {{FLAGS}}; \
    fi

# Build using specified local mode in 10 seconds, then kill the process
@build: (_package-complete)
    if [ {{mode}} = release ]; then \
        cargo check --offline --frozen -q --release -p tinyrasputin; \
        cargo build --offline --frozen -q --release -p tinyrasputin; \
    else \
        cargo check --offline --frozen -p tinyrasputin --features debug_print; \
        cargo build --offline --frozen -p tinyrasputin --features debug_print; \
    fi

@test +FLAGS='': (build)
    if [ {{mode}} = release ]; then cargo test --offline --frozen --release -q -- {{FLAGS}}; else cargo test --offline --frozen -- {{FLAGS}}; fi

@_package-listing-exists:
    test -f .package-list

# Checks if a package is theoretically complete using a generated listing
@_package-complete: (_package-listing-exists)
    while read -r line; do test -f $line; done < .package-list
